library identifier: 'example-pipeline-library', changelog: false

def timestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ")
def version = timestamp.substring(0, 10).replace('-', '')

def elasticsearchUrl = 'https://....ap-northeast-2.es.amazonaws.com'
def indexPrefix = 'elasticsearch'
def awsProfiles = [
    'example-dev'
]
publishBasePipeline slackChannels: ['#devops_alert'], containers: [], {
    currentBuild.displayName = "#${version}"

    stage('Checkout') {
        deleteDir()
        checkout scm
    }
    stage('Prepare reports') {
        httpRequest httpMode: 'DELETE', validResponseCodes: '100:599', url: "${elasticsearchUrl}/${indexPrefix}-${version}"
        domains = []

        withAWS([region: 'ap-northeast-2']) {
            awsProfiles.each { profile ->
                output = sh returnStdout: true, script: """
                    export AWS_PROFILE=${profile} && \
                    aws es list-domain-names
                """
                response = readJSON text: output
                names = response.DomainNames.collect { name -> name.DomainName }

                names.each { name ->
                    output = sh returnStdout: true, script: """
                        export AWS_PROFILE=${profile} && \
                        aws es describe-elasticsearch-domain \
                            --domain-name ${name}
                    """
                    response = readJSON text: output

                    resource = [TagList: []]

                    try {
                        output = sh returnStdout: true, script: """
                            export AWS_PROFILE=${profile} && \
                            aws es list-tags \
                                --arn ${response.DomainStatus.ARN}
                        """
                        resource = readJSON text: output
                    } catch (e) {}
                    
                    domains << ([
                        '@timestamp': timestamp,
                        Account: profile
                    ] << [
                        Tags: resource.TagList.collectEntries { [it.Key, it.Value] }
                    ] << response.DomainStatus)
                }
            }
        }
        writeFile file: "${indexPrefix}-${version}.json", text: domains.collect { '{"index":{}}\n' + groovy.json.JsonOutput.toJson(it) + '\n' }.join('')

        archiveArtifacts artifacts: "${indexPrefix}-${version}.json", onlyIfSuccessful: true
    }
    stage('Publish reports') {
        data = readFile "${indexPrefix}-${version}.json"
        httpRequest acceptType: 'APPLICATION_JSON', contentType: 'APPLICATION_JSON', httpMode: 'POST', requestBody: data, url: "${elasticsearchUrl}/${indexPrefix}-${version}/_bulk"
    }
}