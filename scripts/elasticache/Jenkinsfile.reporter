library identifier: 'example-pipeline-library', changelog: false

def timestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ")
def version = timestamp.substring(0, 10).replace('-', '')

def elasticsearchUrl = 'https://....ap-northeast-2.es.amazonaws.com'
def indexPrefix = 'elasticache'
def awsProfiles = [
    'example-dev'
]
publishBasePipeline slackChannels: ['#devops_alert'], containers: [], {
    currentBuild.displayName = "#${version}"

    stage('Checkout') {
        deleteDir()
        checkout scm
    }
    stage('Prepare reports') {
        httpRequest httpMode: 'DELETE', validResponseCodes: '100:599', url: "${elasticsearchUrl}/${indexPrefix}-${version}"
        clusters = []

        withAWS([region: 'ap-northeast-2']) {
            awsProfiles.each { profile ->
                output = sh returnStdout: true, script: """
                    export AWS_PROFILE=${profile} && \
                    aws sts get-caller-identity
                """
                response = readJSON text: output
                accountId = response.Account

                output = sh returnStdout: true, script: """
                    export AWS_PROFILE=${profile} && \
                    aws elasticache describe-cache-clusters \
                        --show-cache-node-info
                """
                response = readJSON text: output

                response.CacheClusters.each { cluster ->
                    if (cluster.ReplicationGroupId) {
                        output = sh returnStdout: true, script: """
                            export AWS_PROFILE=${profile} && \
                            aws elasticache describe-replication-groups \
                                --replication-group-id ${cluster.ReplicationGroupId}
                        """
                        response = readJSON text: output
                        
                        cluster << [
                            ReplicationGroups: response.ReplicationGroups
                        ]
                    }
                    resource = [TagList: []]

                    try {
                        output = sh returnStdout: true, script: """
                            export AWS_PROFILE=${profile} && \
                            aws elasticache list-tags-for-resource \
                                --resource-name arn:aws:elasticache:ap-northeast-2:${accountId}:cluster:${cluster.CacheClusterId}
                        """
                        resource = readJSON text: output
                    } catch (e) {}

                    clusters << ([
                        '@timestamp': timestamp,
                        Account: profile
                    ] << [
                        Tags: resource.TagList.collectEntries { [it.Key, it.Value] },
                        ClusterName: cluster.ReplicationGroupId ?: cluster.CacheClusterId
                    ] << cluster)
                }
            }
        }
        writeFile file: "${indexPrefix}-${version}.json", text: clusters.collect { '{"index":{}}\n' + groovy.json.JsonOutput.toJson(it) + '\n' }.join('')

        archiveArtifacts artifacts: "${indexPrefix}-${version}.json", onlyIfSuccessful: true
    }
    stage('Publish reports') {
        data = readFile "${indexPrefix}-${version}.json"
        httpRequest acceptType: 'APPLICATION_JSON', contentType: 'APPLICATION_JSON', httpMode: 'POST', requestBody: data, url: "${elasticsearchUrl}/${indexPrefix}-${version}/_bulk"
    }
}